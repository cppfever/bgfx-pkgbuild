# Maintainer: https://github.com/cppfever/bgfx-pkgbuild.git

_realname=bgfx
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.115.7933
pkgrel=1
pkgdesc="Cross-platform rendering library"
url=https://github.com/bkaradzic/bgfx.cmake.git
license=(BSD-2-Clause License)
arch=('any')
mingw_arch=('mingw64')
groups=()
depends=()
makedepends=("git" "${MINGW_PACKAGE_PREFIX}-cmake" "${MINGW_PACKAGE_PREFIX}-ninja")
checkdepends=()
optdepends=()
provides=()
conflicts=()
replaces=()
backup=()
options=()
install=
changelog=
source=("${_realname}::git+https://github.com/bkaradzic/bgfx.cmake.git#tag=v1.115.7933-417f8b8")
noextract=()
md5sums=('SKIP')
validpgpkeys=()

prepare()
{
	cd ${srcdir}/${_realname}
	git submodule update --init --recursive
}

build() {
	mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

	declare -a extra_config
	if check_option "debug" "n"; then
		extra_config+=("-DCMAKE_BUILD_TYPE=Release")
	else
		extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
	fi

	MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
		"${MINGW_PREFIX}"/bin/cmake.exe \
		-GNinja \
		-DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
		-DBGFX_BUILD_TOOLS=OFF -DBGFX_BUILD_EXAMPLES=OFF \
		"${extra_config[@]}" \
		-DBUILD_{SHARED,STATIC}_LIBS=ON \
		../${_realname}

		"${MINGW_PREFIX}"/bin/cmake.exe --build .
}

package() {
	cd "${srcdir}/build-${MSYSTEM}"
	DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .
}